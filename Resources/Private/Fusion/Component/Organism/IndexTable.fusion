prototype(Milly.CrudUI:Component.IndexTable) < prototype(Neos.Fusion:Component) {

    header = ''
    CrudUIView = 'index'
    filterValues = ${[]}
    readonly = false
    hideActions = Neos.Fusion:DataStructure {
        new =  false
        delete = false
        show = false
    }
    disablePagination = true
    modelClassName = ''
    enableInlineEditing = false

    renderer = Neos.Fusion:Tag {
        #tagName = "table"
        attributes.style = 'display: table;'
        attributes.class = Milly.CrudUI:Helper.ThemeRenderer {
            path = 'template.index.table.class'
        }
        content = Neos.Fusion:Join {
            thead = Neos.Fusion:Tag {
                #tagName = "thead"
                attributes.style = 'display: table-header-group;'
                content = Neos.Fusion:Join {
                    headerText = afx`
                        <div @if={props.header}>
                            {props.header}
                        </div>
                    `
                    indexTableHeaderRow = Milly.CrudUI:Component.IndexTableHeaderRow {
                        @if.showColumnHeaders = ${Milly.CrudUI.getConfigurationByModel(props.modelClassName, 'views.'+props.CrudUIView+'.showColumnHeaders')}
                        modelClassName = ${props.modelClassName}
                        CrudUIView = ${props.CrudUIView}
                        showColumnForLabel = ${!props.hideActions.show}
                    }
                }
            }
            tbody = Neos.Fusion:Tag {
                #tagName = "tbody"
                attributes.style = 'display: table-row-group;'
                content = Neos.Fusion:Loop {
                    items = ${props.objects}
                    itemName = "object"
                    itemRenderer = Milly.CrudUI:Component.IndexTableRow {
                        object = ${object}
                        CrudUIView = ${props.CrudUIView}
                        readonly = ${props.readonly}
                        hideActions = ${props.hideActions}
                        enableInlineEditing = ${props.enableInlineEditing}
                    }
                }
            }
            tfoot = Neos.Fusion:Tag {
                @if.notDisabled = ${!props.disablePagination}
                #tagName = "tfoot"
                attributes.style = 'display: table-footer-group;'
                content = Milly.CrudUI:Component.Pagination {
                    modelClassName = ${props.modelClassName}
                    currentPage = ${pagination.currentPage}
                    pageSize = ${pagination.pageSize}
                    lastPage = ${pagination.lastPage}
                    filterValues = ${props.filterValues}
                    @process.wrap = Neos.Fusion:Tag {
                        tagName = 'tr'
                        content = Neos.Fusion:Tag {
                            tagName = 'th'
                            attributes.class = "neos-action"
                            attributes.colspan = "100%"
                            content = ${value}
                        }
                    }
                }
            }
        }
    }

    renderer.@process.prependJs = Neos.Fusion:Join {
        value = ${value}
        js = "<script>
                filterFormElements = document.querySelectorAll('select.filter, input.filter');
                for (let i=0; i<filterFormElements.length; i++){
                    filterFormElements[i].style.display === 'block';
                    filterFormElements[i].addEventListener('change', function(){
                        document.getElementById(this.getAttribute('form')).submit();
                    })
                }

                filterButton = document.getElementById('toggle-filter');
                filterButton.addEventListener('click', function(){
                    filterFormElements = document.querySelectorAll('select.filter, input.filter');
                    for (let i=0; i<filterFormElements.length; i++){
                        elementStyle = filterFormElements[i].style;
                        if (elementStyle.display == 'none') {
                            elementStyle.display = 'block';
                            filterButton.classList.add('neos-active');
                        } else {
                            elementStyle.display = 'none';
                            filterButton.classList.remove('neos-active');
                        }
                    }
                })
            </script>"

    }
}

prototype(Milly.CrudUI:Component.ShowRelations) < prototype(Neos.Fusion:Component) {

    relations = ${[]}
    object = null

    renderer = Neos.Fusion:Loop {
        items = ${props.relations}
        itemKey = "relationName"
        itemName = "relationConfig"

        itemRenderer = afx`
            <Neos.Fusion:Fragment @if={Milly.Reflection.isToOneRelation(props.object, relationName)}>
                <Milly.CrudUI:Component.ActionUi.Show object={Milly.Object.getProperty(props.object, relationName)}
                                                         attributes.class="neos-button" />
            </Neos.Fusion:Fragment>

            <Neos.Fusion:Fragment @if={Milly.Reflection.isToManyRelation(props.object, relationName)}>
                <Milly.CrudUI:Component.IndexTable
                    modelClassName={Milly.Reflection.getTypeOfRelation(props.object, relationName)}
                    CrudUIView="relation"
                    objects={Milly.Object.getProperty(props.object, relationConfig.propertyPath ? relationConfig.propertyPath : relationName)}
                    header={relationConfig.label}
                    disablePagination={!relationConfig.ui.pagination.enable}
                    readonly={relationConfig.readonly}
                    hideActions={relationConfig.hideActions}
                    enableInlineEditing={relationConfig.enableInlineEditing}
                />

                <p @if.condition={!Milly.Object.getProperty(props.object, relationName)}>No {relationConfig.label} created yet.</p>
                <div @if={!relationConfig.readonly && !relationConfig.hideActions.new} class="neos-pull-right" style="margin-bottom:20px">
                    <Milly.CrudUI:Component.ActionUi.New
                        modelClassName={Milly.Reflection.getTypeOfRelation(props.object, relationName)}
                        parent={props.object}
                        showLabel={true}/>
                </div>
            </Neos.Fusion:Fragment>
        `
    }

    @process.debugDump = Neos.Fusion:DebugDump

}

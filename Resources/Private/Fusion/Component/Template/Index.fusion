prototype(Milly.CrudUI:Template.Index) < prototype(Neos.Fusion:Component) {

    @propTypes {
        style = ${PropTypes.oneOf(['narrow', 'wide'])}
        pagination = ${PropTypes.arrayOf(PropTypes.getAny())}
        objects = ${PropTypes.arrayOf(PropTypes.getAny())}
    }

    @context {
        millyCrudTheme = ${this.millyCrudTheme ? this.millyCrudTheme : (millyCrudTheme ? millyCrudTheme : Configuration.setting('Milly.CrudUI.defaultTheme'))}
    }

    style = 'wide'

    header = Neos.Fusion:Join {
        title = Neos.Fusion:Tag {
            tagName = 'legend'
            attributes.style = "display: flex; justify-content: space-between;"
            content = Neos.Fusion:Join {
                label = Milly.CrudUI:Component.Label.ClassLabel {
                    className = ${CrudUIModelClass}
                    showClassLabel = 'many'
                }
                count = ${' (' + objectCount + ')'}
            }
        }
    }

    main = Neos.Fusion:Join {
        filterForm = afx`
            <Neos.Fusion.Form:Form form.target.action="index" attributes.class={'filter'} attributes.id={String.md5(CrudUIModelClass)+'-filter'}/>
        `
        indexTable = Milly.CrudUI:Component.IndexTable {
            objects = ${objects}
            modelClassName = ${CrudUIModelClass}
            disablePagination = ${Milly.CrudUI.getConfigurationByModel(CrudUIModelClass, 'views.index.options.pagination.disable')}
        }
    }

    footer = Neos.Fusion:Join {
        newButton = Milly.CrudUI:Component.ActionUi.New {
            @if.condition = ${hasCreateActions && !Milly.CrudUI.getConfigurationByModel(CrudUIModelClass, 'readonly')}
            object = ${object}
            showLabel = true
            modelClassName = ${CrudUIModelClass}
        }
        export = Milly.CrudUI:Component.ActionUi.ExportCsv
    }

    renderer = Neos.Fusion:Tag {
        attributes.class = Milly.CrudUI:Helper.ThemeRenderer {
            path = 'layout.container.class'
        }
        content = Neos.Fusion:Join {
            header = Neos.Fusion:Tag {
                attributes.class = Milly.CrudUI:Helper.ThemeRenderer {
                    path = 'layout.container.header.class'
                }
                content = ${props.header}
            }
            main = Neos.Fusion:Tag {
                attributes.class = Milly.CrudUI:Helper.ThemeRenderer {
                    path = 'layout.container.main.class'
                }
                content = Neos.Fusion:Join {
                    first = Neos.Fusion:Tag {
                        attributes.class = Neos.Fusion:Case {
                            narrow {
                                condition = ${props.style=='narrow'}
                                renderer = Milly.CrudUI:Helper.ThemeRenderer {
                                    path = 'template.index.main.content.first.narrowClass'
                                }
                            }
                            wide {
                                condition = ${props.style=='wide'}
                                renderer = Milly.CrudUI:Helper.ThemeRenderer {
                                    path = 'template.index.main.content.first.wideClass'
                                }
                            }
                        }
                        content = ${props.main}
                    }
                }
            }
            footer = Neos.Fusion:Tag {
                attributes.class = Milly.CrudUI:Helper.ThemeRenderer {
                    path = 'layout.container.footer.class'
                }
                content = ${props.footer}
            }
        }
        @process.debugDump = Neos.Fusion:DebugDump
    }
}
